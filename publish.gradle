/*
 * Copyright 2000-2021 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'maven-publish'

private addDependencies(publication, dependencyList) {
    publication.pom.withXml {
        def dependencies = asNode().appendNode('dependencies')
        dependencyList.each {
            def dependency = dependencies.appendNode('dependency')
            dependency.appendNode('groupId', it.moduleGroup)
            dependency.appendNode('artifactId', it.moduleName)
            dependency.appendNode('version', it.moduleVersion)
        }
    }
}

private setMavenMetadata(publication, publicationName, publicationDescription) {
    publication.pom {
        name = publicationName
        description = publicationDescription
        url = 'https://github.com/JetBrains/coverage-report'
        licenses {
            license {
                name = 'Apache License, Version 2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0'
            }
        }
        developers {
            developer {
                id = 'JetBrains'
                name = 'JetBrains Team'
                organization = 'JetBrains'
                organizationUrl = 'https://www.jetbrains.com'
            }
        }
        scm {
            connection = 'scm:git:git@github.com:JetBrains/coverage-report.git'
            developerConnection = 'scm:git:ssh:github.com/JetBrains/coverage-report.git'
            url = 'https://github.com/JetBrains/coverage-report'
        }
    }
}

def coverageGroupId = 'org.jetbrains.intellij.deps'
project(":report-builder").afterEvaluate { builder ->
    publishing {
        publications {
            ReportBuilderPublication(MavenPublication) {
                group coverageGroupId
                artifactId report_builder_jar_name
                version version
                artifact builder.tasks.getByName("reportBuilderJar")
                artifact builder.tasks.getByName("reportBuilderSoursesJar")
                artifact builder.tasks.getByName("reportBuilderJavadocJar")
                addDependencies(it, builder.getPublishDependencies())
                setMavenMetadata(owner, "Report Builder", "HTML coverage report generator")
            }
        }
    }
}

ext.publishingUser = System.getenv('PUBLISHING_USER')
ext.publishingPassword = System.getenv('PUBLISHING_PASSWORD')

nexusPublishing {
    repositories {
        sonatype {
            username = rootProject.ext.publishingUser
            password = rootProject.ext.publishingPassword
        }
    }
}
