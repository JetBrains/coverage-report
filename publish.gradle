/*
 * Copyright 2000-2021 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'maven-publish'

private addDependencies(publication, dependencyList) {
    publication.pom.withXml {
        def dependencies = asNode().appendNode('dependencies')
        dependencyList.each {
            def dependency = dependencies.appendNode('dependency')
            dependency.appendNode('groupId', it.moduleGroup)
            dependency.appendNode('artifactId', it.moduleName)
            dependency.appendNode('version', it.moduleVersion)
        }
    }
}

def coverageGroupId = 'org.jetbrains.intellij.deps'
project(":report-builder").afterEvaluate { builder ->
    publishing {
        publications {
            ReportBuilderPublication(MavenPublication) {
                groupId coverageGroupId
                artifactId report_builder_jar_name
                version version
                artifact builder.tasks.getByName("reportBuilderJar")
                addDependencies(it, builder.getPublishDependencies())
            }
            ReportBuilderJavadocPublication(MavenPublication) {
                groupId coverageGroupId
                artifactId report_builder_javadoc_jar_name
                version version
                artifact builder.tasks.getByName("reportBuilderJavadocJar")
            }
        }
        repositories {
            maven {
                url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
                credentials {
                    username = System.getenv('INTELLIJ_DEPENDENCIES_BOT')
                    password = System.getenv('INTELLIJ_DEPENDENCIES_TOKEN')
                }
            }
        }
    }
}
