/*
 * Copyright 2000-2021 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

private addDependencies(publication, project) {
    publication.pom.withXml {
        def dependencies = asNode().appendNode('dependencies')
        project.configurations.getByName("compileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
            def dependency = dependencies.appendNode('dependency')
            dependency.appendNode('groupId', it.moduleGroup)
            dependency.appendNode('artifactId', it.moduleName)
            dependency.appendNode('version', it.moduleVersion)
        }
    }
}

def coverageGroupId = 'org.jetbrains.intellij.deps'
project(":report-builder").afterEvaluate { builder ->
    publishing {
        publications {
            ReportBuilderPublication(MavenPublication) {
                groupId coverageGroupId
                artifactId report_builder_jar_name
                version version
                artifact builder.tasks.getByName("reportBuilderJar")
                addDependencies(it, builder)
            }
            ReportBuilderJavadocPublication(MavenPublication) {
                groupId coverageGroupId
                artifactId report_builder_javadoc_jar_name
                version version
                artifact builder.tasks.getByName("reportBuilderJavadocJar")
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publish = true

    pkg {
        repo = 'intellij-third-party-dependencies'
        name = 'report-builder'
        userOrg = 'jetbrains'
        version {
            name = project.version
        }
    }
    publications = ['ReportBuilderPublication', 'ReportBuilderJavadocPublication']
}
